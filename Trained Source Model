TITLE '+Trained Source Model'
COORDINATES
	Cartesian1
VARIABLES
l (threshold=0.001), a (threshold=0.001), c (threshold=0.001), m (threshold=0.001), f (threshold=0.001)
SELECT
ERRLIM=1e-3
threads=1
DEFINITIONS
	
    {Dl and Da from rescaled experimental values,
    Dk assumed same as Da,
    Dm assumed many orders smaller than Dl}
    Dl = 5.2e1		!5.2e1, from Dabagh rescaled to 24 hr time and space according to Chalmers
    Da = 5.4e3		!5.4e3, from Paavola rescaled to 24 hr time and space according to Chalmers
    Dc = 5.4e3		!5.4e3, assumed the same as Da 
    Dm = 5.2e-3 	! Previously used value: 1.0e-2	!Chalmers assumes 4 orders of magnitude smaller than Dl, simple time rescale gives 5.2e-3, but should move with change to Dl  
    
    {decay taken to be much smaller than other terms,
    values scaled to time,
    d_m assumed orders smaller than others}
    !less decay than in Chalmers
    d_l = 5.0e-1 	!5.2e-1, time rescale, Chalmers assumes much smaller than other quantities
    d_a = 5.0e-1	!5.2e-1, time rescale ""	
    d_c = 5.0e-1	!5.2e-1, time rescale ""
    d_m = 5.0e-3	!5.2e-3, time rescale, Chalmers assumes 2 orders smaller than other decays
    
    {Mu_l replaced by E_i rescalar, 
    Mu_a, Mu_c, Mu_f rescaled to time and E_i}
    Mu_a = 4.0e1 !chemoattractants per macrophage per % influx
    Mu_c = 4.0e0 !cytokines per macrophage
    Mu_f = 4.0e-2 !ldl to foam cell designation !og time rescale: 4.1e-2
    
    {All rescaled to time}
    Sigma_a1 = 5.0e2	!5.2e2, time rescale
    Sigma_a2 = 5.0e0	!5.2e1, time rescale
    Sigma_c = 5.0e-3	!5.2e-2, time rescale
    Xm = 5.0e-3		!5.2e-3, time rescale
    
   {Constants with no time dependence}
    A0 = 1.0e-1 !same as Chalmers
    C0 = 1.0e-2 !same as Chalmers
    Beta_a = 1.0e0 !same as Chalmers
    
    {Parameters rescaled to time}
    Sigma_l = 4.0e1			!og time rescale: 1.0e1		
    Sigma_m = 1.0e-5		!og time rescale: 2.6e-6 - 4.1e-6 
    {V6 1.0e-6, 4.0e-6, 7.0e-6, 1.0e-5}
    
    {V5 5.0e-7, 1.5e-6, 2.5e-6 3.5e-6}
    {V4 3.0e-6, 1.2e-5, 2.1e-5, 3.0e-5}
    {V3 3.0e-6, 6.0e-6, 9.0e-6, 3.0e-5}
    {3.0e-5, 6.0e-5, 9.0e-5, 1.2e-4 high}
    
    {Epigenetic training modifiers}
    E_a = 3
    E_c = 4
    E_i = 116.28 * (ln(l + 7.6) - 1.1) !84.21
    E_e = 78.00 * (ln(l + 4.6) - 0.4) !45.46
    
    Influx = m * l * E_i  / (1.0 + l)
    Efflux = m * l * E_e / (1.0 + l)
    
    ldlc = Influx
    ldlci = Influx / m

EQUATIONS
	a:		Da*del2(a) = -E_a  * Mu_a * Influx + d_a * a {decay} + dt(a)

	c:		Dc*del2(c) = -E_c * Mu_c * Influx + d_c * c {decay} + dt(c)
	
	l:		Dl*del2(l) = Influx {consumption} + d_l * l {decay} + dt(l)
	
	m:	Dm*del2(m) = Xm * div(m * grad(l)) {chemotaxis} + dt(f) {conversion} + d_m * m {dedifferentiation} + dt(m)
    
    f:		dt(f) = Mu_f  * (Influx {influx} - Efflux) {efflux}

BOUNDARIES
	REGION 1
        START(0)
			POINT LOAD(a) = Sigma_a1 * l / (Beta_a + l) + Sigma_a2 * c
			POINT LOAD(c) = -Sigma_c * c
			POINT LOAD(l) = Sigma_l
			POINT LOAD(m) = IF (a < A0) THEN 0 ELSE Sigma_m * (1.0 + C0 * c) * (a - A0)
			POINT LOAD(f) = 0
		LINE TO(1)
			POINT LOAD(a)=0
			POINT LOAD(c)=0
			POINT LOAD(l)=0
			POINT LOAD(m)=0
			POINT LOAD(f)=0

TIME 0 TO 90
plots
	FOR T=0 BY 1 TO 90
    
{MONITORS
	FOR T = 0 BY 0.01 TO 4
    history(influx) at (0)
    history(efflux) at (0)
    history(influx - efflux) at (0)
    !history(del2(l)) at (0)
    !history(dt(l)) at (0)
    !elevation(dxx(l)) from (0) to (1) 
    history(E_i) at (0)
    history(E_e) at (0)}
    
histories
    history(l) at (0) export file "l(0)_+TH6Sigmam_4"				{Modified LDL Endotheilal Boundary}
    history(a) at (0) export file "a(0)_+TH6Sigmam_4"				{Chemoattractants on Endothelial Boundary}
    history(c) at (0) export file "c(0)_+TH6Sigmam_4"				{ES Cytokines on Endothelial Boundary}
	history(integral(m)) export file "int(m)_+TH6Sigmam_4"	{Monocytes / Macrophages Intimal Total}
	history(integral(f)) export file "int(f)_+TH6Sigmam_4"		{Foam Cells Intimal Total}

END
